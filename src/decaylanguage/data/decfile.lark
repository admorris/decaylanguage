// Copyright (c) 2018-2023, Eduardo Rodrigues and Henry Schreiner.
//
// Distributed under the 3-clause BSD license, see accompanying file LICENSE
// or https://github.com/scikit-hep/decaylanguage for details.

start : _NEWLINE* (line _NEWLINE+)+ ("End" _NEWLINE+)?
?line : define | particle_def | pythia_def | jetset_def | ls_def | model_alias | alias | chargeconj | commands | decay | cdecay | copydecay | setlspw | setlsbw | changemasslimit

pythia_def : ("PythiaBothParam" | "PythiaAliasParam") LABEL ":" LABEL "=" (LABEL | SIGNED_NUMBER)

jetset_def : "JetSetPar" LABEL "=" SIGNED_NUMBER

ls_def : ("LSFLAT" | "LSNONRELBW") label

setlsbw : "BlattWeisskopf" label value

setlspw : "SetLineshapePW" label label label value

cdecay : "CDecay" label

define : "Define" label value

particle_def: "Particle" label value value

model_alias : "ModelAlias" model_label model

alias : "Alias" label label

chargeconj : "ChargeConj" label label

changemasslimit : ("ChangeMassMin" | "ChangeMassMax") label value

?commands : global_photos

global_photos : boolean_photos

boolean_photos : "yesPhotos" -> yes
                | "noPhotos"  -> no

decay : "Decay" particle _NEWLINE+ decayline+ "Enddecay"
decayline : value particle* photos? model _NEWLINE+
value : SIGNED_NUMBER
photos : "PHOTOS"

copydecay : "CopyDecay" label label

label : LABEL
particle : LABEL // Add full particle parsing here
model_label : LABEL

model : (model_label _SEMICOLON+ | MODEL_NAME_AND_SC | MODEL_NAME_AND_WS model_options _SEMICOLON+)
model_options : (value | LABEL | _NEWLINE | _COMMA)+

// model : model_generic
// model_helamp : "HELAMP" (SIGNED_NUMBER SIGNED_NUMBER)+

// Terminal definitions
// To use a fast parser, we need to avoid conflicts

%import common.WS_INLINE
%import common.SIGNED_NUMBER

// Disregard comments, (multiple) newlines and whitespace in parser tree
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )
_SEMICOLON: /;/
_COMMA: /,/
_WS: WS_INLINE

// Model names must either be followed by at least one whitespace (when they have model parameters) or a semicolon (without model parameters)
// We must set priorities here to use lalr - match model name above label, and label above something else

MODEL_NAME_AND_WS.2: MODEL_NAME _WS+
MODEL_NAME_AND_SC.2: MODEL_NAME _WS* _SEMICOLON+

MODEL_NAME : "BCL"|"BC_SMN"|"BC_TMN"|"BC_VHAD"|"BC_VMN"|"BGL"|"BLLNUL"|"BNOCB0TO4PICP"|"BNOCBPTO3HPI0"|"BNOCBPTOKSHHH"|"BQTOLLLL"|"BQTOLLLLHYPERCP"|"BSTOGLLISRFSR"|"BSTOGLLMNT"|"BS_MUMUKK"|"BT02PI_CP_ISO"|"BTO3PI_CP"|"BTODDALITZCPK"|"BTOSLLALI"|"BTOSLLBALL"|"BTOSLLMS"|"BTOSLLMSEXT"|"BTOVLNUBALL"|"BTOXELNU"|"BTOXSGAMMA"|"BTOXSLL"|"BToDiBaryonlnupQCD"|"BaryonPCR"|"CB3PI-MPP"|"CB3PI-P00"|"D0GAMMADALITZ"|"DToKpienu"|"D_DALITZ"|"D_hhhh"|"ETAPRIME_DALITZ"|"ETA_DALITZ"|"ETA_FULLDALITZ"|"ETA_LLPIPI"|"ETA_PI0DALITZ"|"FLATQ2"|"FLATSQDALITZ"|"FOURBODYPHSP"|"GENERIC_DALITZ"|"GOITY_ROBERTS"|"HELAMP"|"HQET"|"HQET2"|"HQET3"|"ISGW"|"ISGW2"|"KS_PI0MUMU"|"LLSW"|"LNUGAMMA"|"LQCD"|"Lb2Baryonlnu"|"Lb2plnuLCSR"|"Lb2plnuLQCD"|"LbAmpGen"|"MELIKHOV"|"OMEGA_DALITZ"|"PARTWAVE"|"PHI_DALITZ"|"PHSP"|"PHSPDECAYTIMECUT"|"PHSPFLATLIFETIME"|"PI0_DALITZ"|"PROPSLPOLE"|"PTO3P"|"PVV_CPLH"|"PYCONT"|"PYTHIA"|"SLBKPOLE"|"SLL"|"SLN"|"SLPOLE"|"SSD_CP"|"SSD_DirectCP"|"SSS_CP"|"SSS_CPT"|"SSS_CP_PNG"|"STS"|"STS_CP"|"SVP"|"SVP_CP"|"SVP_HELAMP"|"SVS"|"SVS_CP"|"SVS_CPLH"|"SVS_CP_ISO"|"SVS_NONCPEIGEN"|"SVVHELCPMIX"|"SVV_CP"|"SVV_CPLH"|"SVV_HELAMP"|"SVV_NONCPEIGEN"|"TAUHADNU"|"TAULNUNU"|"TAUOLA"|"TAUSCALARNU"|"TAUVECTORNU"|"THREEBODYPHSP"|"TSS"|"TVP"|"TVS_PWAVE"|"VLL"|"VSP_PWAVE"|"VSS"|"VSS_BMIX"|"VSS_MIX"|"VTOSLL"|"VUB"|"VVP"|"VVPIPI"|"VVS_PWAVE"|"XLL"|"YMSTOYNSPIPICLEO"|"YMSTOYNSPIPICLEOBOOST"|"doKm"|"imqp"

LABEL : /[a-zA-Z0-9\/\-+*_()'~]+/
COMMENT : /[#][^\n]*/

// We should ignore comments
%ignore COMMENT

// Disregard spaces in text
%ignore WS_INLINE
